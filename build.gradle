plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    group = rootProject.maven_group
    version = rootProject.mod_version
    base.archivesName = rootProject.archives_base_name
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'maven-publish'

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
        it.options.encoding = "UTF-8"
    }

    repositories {
        maven { name = "TerraformersMC"; url = "https://maven.terraformersmc.com/" }
        maven { name = "FzzyMaven"; url = "https://maven.fzzyhmstrs.me/" }
        maven { name = "CurseMaven"; url = "https://cursemaven.com" }
        maven { name = "Modrinth"; url = "https://api.modrinth.com/maven" }
        maven { name = "GeckoLib"; url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
        maven { url = "https://thedarkcolour.github.io/KotlinForForge/" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:$rootProject.minecraft_version"
        mappings loom.layered {
            it.mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }
        repositories {
            // Add publishing repositories here
        }
    }
}